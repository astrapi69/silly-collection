
plugins {
    id "io.franzbecker.gradle-lombok" version "3.2.0"
    id "java"
    id "maven-publish"
    id "signing"
    id "com.github.ben-manes.versions" version "0.27.0"
}

apply plugin: "jacoco"

group = "de.alpharogroup"
version = "5.7-SNAPSHOT"
description = "silly-collections"
sourceCompatibility = "1.8"

repositories {
    jcenter()
    mavenLocal()
    mavenCentral()
}

ext {
    lombokVersion = "1.18.10"
    commonsCollections4Version = "4.4"
    jobjCompareVersion = "3.2"
    jobjCloneVersion = "3.2"
    jobjContractVerifierVersion = "3.2"
    vintageTimeVersion = "5.1"
    testObjectsVersion = "5.2"
    randomBeansVersion = "3.9.0"
    jmhVersion = "1.22"
    meanbeanVersion = "2.0.3"
    testngVersion = "7.1.0"
    junitVersion = "4.13-rc-2"
    mockitoCoreVersion = "3.2.0"
}

dependencies {
    compile "org.projectlombok:lombok:${lombokVersion}"
    compile "org.apache.commons:commons-collections4:${commonsCollections4Version}"
    compile "de.alpharogroup:jobj-compare:${jobjCompareVersion}"
    testCompile "de.alpharogroup:jobj-clone:${jobjCloneVersion}"
    testCompile "de.alpharogroup:jobj-contract-verifier:${jobjContractVerifierVersion}"
    testCompile "de.alpharogroup:vintage-time:${vintageTimeVersion}"
    testCompile "de.alpharogroup:test-objects:${testObjectsVersion}"
    testCompile "io.github.benas:random-beans:${randomBeansVersion}"
    testCompile "org.openjdk.jmh:jmh-core:${jmhVersion}"
    testCompile "org.openjdk.jmh:jmh-generator-annprocess:${jmhVersion}"
    testCompile "org.meanbean:meanbean:${meanbeanVersion}"
    testCompile "org.testng:testng:${testngVersion}"
    testCompile "junit:junit:${junitVersion}"
    testCompile "org.mockito:mockito-core:${mockitoCoreVersion}"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = "${rootProject.name}"
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = "${rootProject.name}"
                description = "Utility library for collections, comparators and iterator classes"
                url = "https://github.com/lightblueseas/"+"${rootProject.name}"
                organization {
                    name = "Alpha Ro Group UG (haftungsbeschr√§ngt)"
                    url = "http://www.alpharogroup.de/"
                }
                issueManagement {
                    system = "GitHub"
                    url = "https://github.com/lightblueseas/"+"${rootProject.name}"+"/issues"
                }
                licenses {
                    license {
                        name = "MIT License"
                        url = "http://www.opensource.org/licenses/mit-license.php"
                        distribution = "repo"
                    }
                }
                developers {
                    developer {
                        id = "astrapi69"
                        name = "Asterios Raptis"
                    }
                }
                scm {
                    connection = "scm:git:git:@github.com:lightblueseas/"+"${rootProject.name}"+".git"
                    developerConnection = "scm:git:git@github.com:lightblueseas/"+"${rootProject.name}"+".git"
                    url = "git:@github.com:lightblueseas/"+"${rootProject.name}"+".git"
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username System.getenv("ossrhUsername") ?: project.property("ossrhUsername")
                password System.getenv("ossrhPassword") ?: project.property("ossrhPassword")
            }
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption("html5", true)
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

test {
    useTestNG()
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

check.dependsOn jacocoTestReport
